plugins {
    id 'org.jgrapes.java-bundle-conventions'
    id 'com.github.node-gradle.node'
}

dependencies {
  api project(':org.jgrapes.webconsole.base')
  runtimeOnly project(':org.jgrapes.webconsole.provider.solidjs')
}

task compileTs(type: NodeTask) {
    dependsOn ':npmInstall'
    dependsOn ':org.jgrapes.webconsole.base:buildLib'
    dependsOn ':org.jgrapes.webconsole.provider.solidjs:processResources'
    inputs.files project.fileTree(dir: 'src', include: '**/*.ts')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.mjs')
    outputs.dir project.file('build/generated/resources')
    script = file("${rootProject.rootDir}/node_modules/rollup/dist/bin/rollup")
    args = ["-c"]
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn compileTs
}

eclipse {
    autoBuildTasks compileTs
}

// Prevents deadlock.
generatePomFileForMavenPublication {
    mustRunAfter ':org.jgrapes.webconsole.base:jar'
}
