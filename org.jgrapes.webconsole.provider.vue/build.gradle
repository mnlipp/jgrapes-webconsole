dependencies {
	api project(':org.jgrapes.webconsole.base')
}

ext.releaseVersion = "0.6.0"
ext.isSnapshot = false
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

apply plugin: 'com.github.node-gradle.node'

task prepareNpmLibs(type: Copy) {
    dependsOn 'npmInstall'
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/provider/vue/vue')
    from ('node_modules/vue/dist') {
        include 'vue.d.ts'
        filter({ line -> line.replace('@vue/', './')} )
    }
    from ('node_modules/vue/dist') {
        include '*-browser.*'
    }
    from ('node_modules/@vue/runtime-core/dist') {
        include '*.d.ts'
    }
    from ('node_modules/@vue/runtime-dom/dist') {
        include '*.d.ts'
    }
    doLast {
        new File(projectDir, 
            'build/generated/resources/org/jgrapes/webconsole/provider/vue/vue/vue.d.ts').text = """
export * from "./runtime-core";
export * from "./runtime-dom";

export { }
"""
    }
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn 'prepareNpmLibs'
}

eclipse {
    synchronizationTasks prepareNpmLibs
}
