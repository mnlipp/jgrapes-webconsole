plugins {
    id 'org.jgrapes.java-bundle-conventions'
    id 'com.github.node-gradle.node'
}

dependencies {
	api project(':org.jgrapes.webconsole.base')
}

task prepareNpmLibs(type: Copy) {
    dependsOn(':npmInstall')
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/provider/lodash/lodash')
    from file("node_modules/lodash/lodash.js")
    from file("node_modules/lodash/lodash.min.js")
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn 'prepareNpmLibs'
}

eclipse {
    synchronizationTasks prepareNpmLibs
}
