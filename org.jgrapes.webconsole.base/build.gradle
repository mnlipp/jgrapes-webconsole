dependencies {
    // BndPomRepository (3.5.0) cannot handle ranges without upper limit
    api 'org.jgrapes:org.jgrapes.core:[1.22.1,2)'
    api 'org.jgrapes:org.jgrapes.util:[1.38.0,2)'
    api 'org.jgrapes:org.jgrapes.io:[2.12.1,3)'
    api 'org.jgrapes:org.jgrapes.http:[3.5.0,4)'
    api 'org.freemarker:freemarker:[2.3.31,2.4)'
    api 'com.fasterxml.jackson.core:jackson-databind:2.18.0'
    api 'jakarta.mail:jakarta.mail-api:[2.1.3,3)'
}

apply plugin: 'com.github.node-gradle.node'

task buildLib(type: NodeTask, dependsOn: ':npmInstall') {
    inputs.files(project.fileTree(dir: 'src', include: '**/*.ts').flatten())
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.mjs')
    outputs.cacheIf { true }
    outputs.dir project.file('build/generated/resources')
    script = file("${rootProject.rootDir}/node_modules/rollup/dist/bin/rollup")
    args = ["-c"]
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources.dependsOn 'buildLib'

eclipse {
    autoBuildTasks buildLib
}

ext.jsDocDestDir = "${rootProject.buildDir}/javadoc/org/jgrapes/webconsole/base/jsdoc"

task tsdoc(type: NodeTask, dependsOn: ':npmInstall') {
    inputs.dir project.file('src')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('typedoc.json')
    outputs.dir project.file(jsDocDestDir)
    script = file("${rootProject.rootDir}/node_modules/typedoc/bin/typedoc")
    args = []
}

configurations {
    markdownDoclet
    javadocTaglets
}
 
dependencies {
    markdownDoclet "org.jdrupes.mdoclet:doclet:4.2.0"
    javadocTaglets "org.jdrupes.taglets:plantuml-taglet:3.1.0"
}

task javadocResources(type: Copy) {
    into file("${rootProject.buildDir}/javadoc")
    from (rootProject.rootDir) {
        include 'WebConsole-pic1.png'
    }
}

task apidocs(type: JavaExec) {
    // Does not work on JitPack, no /usr/bin/dot
    enabled = JavaVersion.current() == JavaVersion.VERSION_21 && !isJitPackBuild
    
    dependsOn javadocResources
    dependsOn classes
    dependsOn tsdoc
    inputs.file "${rootProject.rootDir}/overview.md"
    inputs.file "${rootProject.rootDir}/misc/javadoc-overwrites.css"

    classpath files(rootProject.subprojects
        .findAll {project -> project.pluginManager.hasPlugin('java-library')
            || project.pluginManager.hasPlugin('application') }
        .collect {project -> project.sourceSets.main.compileClasspath})
    var sourceDirs = rootProject.subprojects
        .findAll({project -> project.name.startsWith("org.jgrapes")})
        .collect{project -> project.sourceSets.main.allJava.getSrcDirs() }
        .flatten().findAll(dir -> dir.exists())
    sourceDirs.each { inputs.dir(it) }
            
    ext.destinationDir = file("${rootProject.buildDir}/javadoc")
    outputs.dir(destinationDir)
    
    jvmArgs = ['--add-exports=jdk.compiler/com.sun.tools.doclint=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.code=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
        '--add-exports=jdk.compiler/com.sun.tools.javac.util=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.tool=ALL-UNNAMED',
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit=ALL-UNNAMED',
        '--add-opens=jdk.javadoc/jdk.javadoc.internal.doclets.toolkit.resources.releases=ALL-UNNAMED',
        '-Duser.language=en', '-Duser.region=US']
    mainClass = 'jdk.javadoc.internal.tool.Main'
    
    args = ['-doctitle', "JGrapes Web Console ${project.version}",
        '-use',
        '-linksource',
        '-link', 'https://docs.oracle.com/en/java/javase/21/docs/api/',
        '-link', 'https://mnlipp.github.io/jgrapes/latest-release/javadoc/',
        '-link', 'https://freemarker.apache.org/docs/api/',
        '--add-exports', 'jdk.javadoc/jdk.javadoc.internal.tool=ALL-UNNAMED',
        '--add-exports', 'jdk.compiler/com.sun.tools.javac.tree=ALL-UNNAMED',
        '-docletpath', configurations.markdownDoclet.files.asType(List).join(":"),
        '-doclet', 'org.jdrupes.mdoclet.MDoclet',
        '-tagletpath', configurations.javadocTaglets.files.asType(List).join(":"),
        '-taglet', 'org.jdrupes.taglets.plantUml.PlantUml',
        '-taglet', 'org.jdrupes.taglets.plantUml.StartUml',
        '-taglet', 'org.jdrupes.taglets.plantUml.EndUml',
        '-overview', "${rootProject.rootDir}/overview.md",
        '-d', destinationDir,
        '-sourcepath', sourceDirs.join(':'),
        '-subpackages', 'org.jgrapes',
        '-bottom', file("${rootProject.rootDir}/misc/javadoc.bottom.txt").text,
        '--allow-script-in-comments',
        '-Xdoclint:-html',
        '--add-stylesheet', "${rootProject.rootDir}/misc/javadoc-overwrites.css",
        '--add-exports=jdk.javadoc/jdk.javadoc.internal.doclets.formats.html=ALL-UNNAMED',
        '-quiet'
        ]

    ignoreExitValue true
    
}

task buildDoc {
    group 'build'
    dependsOn 'tsdoc'
    dependsOn 'apidocs'
}

task javadocJar(type: Jar) {
    dependsOn buildDoc
    from apidocs.destinationDir
    archiveClassifier = "javadoc"
}
