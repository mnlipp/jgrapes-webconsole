buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.github.ksoichiro:gradle-web-resource-plugin:1.7.3'
    }
}

dependencies {
    // BndPomRepository (3.5.0) cannot handle ranges without upper limit
    compile 'org.jgrapes:org.jgrapes.http:[1.1.1,2)'
    compile 'org.jgrapes:org.jgrapes.util:[1.1.1,2)'
    //  compile 'com.github.mnlipp:jgrapes:master-SNAPSHOT'
    compile 'org.jdrupes.json:json:[2.0.0,2.1.0)'
    compile 'org.freemarker:freemarker:[2.3.27-incubating,2.4)'
    runtimeOnly project(':org.jgrapes.webconsole.provider.jquery')
}

ext.releaseVersion = "0.30.0"
ext.isSnapshot = false
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

jar {
    into('META-INF') {
        from 'resources/META-INF'
    }
    into('.') {
        from 'build/generated/resources'
    }
}

task javadocJar(type: Jar) {
    from (javadoc)
    classifier "javadoc"
}

artifacts {
    archives javadocJar
}

apply plugin: 'com.moowork.node'

task buildLib(type: NpmTask) {
    dependsOn('npm_install')
    inputs.dir project.file('resources')
    inputs.file project.file('package.json')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.js')
    outputs.dir project.file('build/generated/resources')
    args = ["run", "build"]
}

sourceSets.main.resources.srcDir 'build/generated/resources'

eclipse {
    autoBuildTasks buildLib
}

ext.jsDocDestDir = "${rootProject.buildDir}/javadoc/org/jgrapes/webconsole/base/jsdoc"

task tsdoc(type: NpmTask) {
    dependsOn 'npm_install'
    inputs.dir project.file('src')
    inputs.file project.file('package.json')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.js')
    outputs.dir project.file(jsDocDestDir)
    args = ["run", "build:doc"]
}

configurations {
    markdownDoclet
}
 
dependencies {
    markdownDoclet "org.jdrupes.mdoclet:doclet:2.0.0"
}

configurations {
    javadocTaglets
}
 
dependencies {
    javadocTaglets "org.jdrupes.taglets:plantuml-taglet:2.0.0"
}

task java11doc(type: JavaExec) {
    enabled = JavaVersion.current().isJava11() && !isJitPackBuild
    
    dependsOn classes
    inputs.file "${rootProject.rootDir}/overview.md"
    ext.destinationDir = file("${rootProject.buildDir}/javadoc")

    jvmArgs = ['--add-exports=jdk.javadoc/jdk.javadoc.internal.tool=ALL-UNNAMED']
    classpath files(rootProject.subprojects.collect {project ->
        project.sourceSets.main.compileClasspath})    
    main = 'jdk.javadoc.internal.tool.Main'
    args = ['-doctitle', "JGrapes Web Console ${project.version}",
        '-overview', "${rootProject.rootDir}/overview.md",
        '-use',
        '-linksource',
        '-link', 'https://docs.oracle.com/en/java/javase/11/docs/api/',
        '-link', 'https://mnlipp.github.io/jgrapes/latest-release/javadoc/',
        '--add-exports', 'jdk.javadoc/jdk.javadoc.internal.tool=ALL-UNNAMED',
        '-docletpath', configurations.markdownDoclet.files.asType(List).join(":"),
        '-doclet', 'org.jdrupes.mdoclet.MDoclet',
        '-tagletpath', configurations.javadocTaglets.files.asType(List).join(":"),
        '-taglet', 'org.jdrupes.taglets.plantUml.PlantUml',
        '-taglet', 'org.jdrupes.taglets.plantUml.StartUml',
        '-taglet', 'org.jdrupes.taglets.plantUml.EndUml',
        '-d', destinationDir,
        '-bottom', file("${rootProject.rootDir}/misc/javadoc.bottom.txt").text,
        '--allow-script-in-comments',
        '-Xdoclint:-html',
        '-quiet'
        ]

    rootProject.subprojects.findAll({project ->
        project.name.startsWith("org.jgrapes.") && project.name != "org.jgrapes.webcon"}) \
        .collect {project -> project.sourceSets.main.allJava }.each {
            args it
        }

    ignoreExitValue true
    
}

task buildDoc(type: Task) {
    dependsOn 'java11doc'
    dependsOn 'tsdoc'
}