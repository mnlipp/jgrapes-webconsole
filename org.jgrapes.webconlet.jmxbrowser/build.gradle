plugins {
    id 'org.jgrapes.java-bundle-conventions'
    id 'com.github.node-gradle.node'
}

dependencies {
	api project(':org.jgrapes.webconsole.base')
    api 'org.jdrupes.json:json:[2.3.1,2.4.0)'
    runtimeOnly project(':org.jgrapes.webconsole.provider.vue')
    runtimeOnly project(':org.jgrapes.webconsole.provider.forkawesome')
}

release {
    enabled = false
}

task compileTs(type: NodeTask) {
    dependsOn ':npmInstall'
    dependsOn ':org.jgrapes.webconsole.base:buildLib'
    dependsOn ':org.jgrapes.webconsole.provider.vue:npmInstall'
    dependsOn ':org.jgrapes.webconsole.provider.jgwcvuecomponents:compileTs'
    inputs.files project.fileTree(dir: 'src', include: '**/*.ts')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.mjs')
    outputs.dir project.file('build/generated/resources')
    script = file("${rootProject.rootDir}/node_modules/rollup/dist/bin/rollup")
    args = ["-c"]
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn compileTs
}

release {
    enabled = false
}

afterEvaluate {
    tasks.matching { it.group == "publishing" }.all {
        enabled = false
    }
}

eclipse {
    autoBuildTasks compileTs
}
