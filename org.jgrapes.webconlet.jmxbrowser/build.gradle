dependencies {
	api project(':org.jgrapes.webconsole.base')
    runtimeOnly project(':org.jgrapes.webconsole.provider.vue')
    runtimeOnly project(':org.jgrapes.webconsole.provider.fontawesome')
}

ext.releaseVersion = "0.1.0"
ext.isSnapshot = true
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

apply plugin: 'com.github.node-gradle.node'

task buildLib(type: NodeTask) {
    dependsOn ':npmInstall'
    dependsOn ':aash-vue-components:buildLib'
    dependsOn ':org.jgrapes.webconsole.base:buildLib'
    dependsOn ':org.jgrapes.webconsole.provider.vue:npmInstall'
    inputs.files project.fileTree(dir: 'src', include: '**/*.ts')
    inputs.files project.fileTree(dir: 'src', include: '**/*.vue')
    inputs.files project.fileTree(dir: 'src', include: '**/*.scss')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.js')
    outputs.cacheIf { true }
    outputs.dir project.file('build/generated/resources')
    script = file("${rootProject.rootDir}/node_modules/rollup/dist/bin/rollup")
    args = ["-c"]
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn 'buildLib'
}

eclipse {
    autoBuildTasks buildLib
}
