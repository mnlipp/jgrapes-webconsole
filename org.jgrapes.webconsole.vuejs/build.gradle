buildscript {
	repositories {
		mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
	}
	dependencies {
		classpath 'com.github.ksoichiro:gradle-web-resource-plugin:1.7.3'
        classpath "gradle.plugin.org.kravemir.gradle.sass:GradleSassPlugin:1.2.2"
	}
}

import org.kravemir.gradle.sass.SassCompileTask;

apply plugin: 'com.github.node-gradle.node'

dependencies {
    api project(':org.jgrapes.webconsole.base')
    runtimeOnly project(':org.jgrapes.webconsole.provider.vue')
    runtimeOnly project(':org.jgrapes.webconsole.provider.jgwcvuecomponents')
    runtimeOnly project(':org.jgrapes.webconsole.provider.forkawesome')
    runtimeOnly project(':org.jgrapes.webconsole.provider.jqueryui')
    runtimeOnly project(':org.jgrapes.webconsole.provider.gridstack')
    runtimeOnly project(':org.jgrapes.webconsole.provider.jquitouchpunch')
}

task prepareNpmLibs(type: Copy) {
    dependsOn 'npmInstall'
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/vuejs/lib')
    from file("node_modules/normalize.css/normalize.css")
}

task buildLib(type: NodeTask) {
    dependsOn ':npmInstall'
    dependsOn ':aash-vue-components:buildLib'
    dependsOn ':org.jgrapes.webconsole.base:buildLib'
    dependsOn ':org.jgrapes.webconsole.provider.vue:npmInstall'
    dependsOn ':org.jgrapes.webconsole.provider.gridstack:npmInstall'
    inputs.dir project.file('src')
    inputs.file project.file('tsconfig.json')
    inputs.file project.file('rollup.config.js')
    outputs.dir project.file('build/generated/resources')
    script = file("${rootProject.rootDir}/node_modules/rollup/dist/bin/rollup")
    args = ["-c"]
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn prepareNpmLibs
    dependsOn buildLib
}

eclipse {
    autoBuildTasks buildLib
    synchronizationTasks prepareNpmLibs 
}

ext.jsDocDestDir = "${rootProject.buildDir}/javadoc/org/jgrapes/webconsole/vuejs/jsdoc"

task jsdoc(type: NodeTask) {
    dependsOn(':npm_install')
    new File(jsDocDestDir).mkdirs()
    script = file("${rootProject.projectDir}/node_modules/jsdoc/jsdoc.js")
    args = ["-c", "${rootProject.projectDir}/jsdoc.json",
        "--destination", jsDocDestDir,
        "resources/org/jgrapes/webconsole/vuejs/vuejsconsole.js"]
}
