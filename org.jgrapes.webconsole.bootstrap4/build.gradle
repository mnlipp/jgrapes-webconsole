buildscript {
	repositories {
		mavenCentral()
	}
	dependencies {
		classpath 'com.github.ksoichiro:gradle-web-resource-plugin:1.7.3'
	}
}

artifacts {
	archives sourcesJar
	archives javadocJar
}

dependencies {
    compile project(':org.jgrapes.webconsole.base')
    runtimeOnly project(':org.jgrapes.webconsole.provider.fontawesome')
    runtimeOnly project(':org.jgrapes.webconsole.provider.jquery')
    runtimeOnly project(':org.jgrapes.webconsole.provider.jqueryui')
    runtimeOnly project(':org.jgrapes.webconsole.provider.gridstack')
    runtimeOnly project(':org.jgrapes.webconsole.provider.jquitouchpunch')
}

ext.releaseVersion = "0.35.0"
ext.isSnapshot = true
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

jar {
    into('META-INF') {
        from 'resources/META-INF'
    }
}

apply plugin: 'com.moowork.node'

task prepareNpmLibs(type: Copy) {
    mustRunAfter 'npm_install'
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/bootstrap4/lib/bootstrap')
    from ("node_modules/bootstrap/dist") {
        include "**/*"
    }
}

processResources.dependsOn 'npm_install'
processResources.dependsOn 'prepareNpmLibs'

sourceSets.main.resources.srcDir 'build/generated/resources'

eclipse {
    autoBuildTasks prepareNpmLibs
}

apply plugin: 'com.moowork.node'

ext.jsDocDestDir = "${rootProject.buildDir}/javadoc/jsdoc"

task jsdoc(type: NodeTask) {
    dependsOn(':npm_install')
    new File(jsDocDestDir).mkdirs()
    script = file("${rootProject.projectDir}/node_modules/jsdoc/jsdoc.js")
    args = ["-c", "${rootProject.projectDir}/jsdoc.json",
        "--destination", jsDocDestDir,
        "resources/org/jgrapes/webconsole/bootstrap4/b4uiconsole.js"]
}
