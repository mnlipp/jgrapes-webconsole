// Additional jars for publishing

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    classifier "sources"
}

// MavenPublishing (new)

// Prepare gitlab authentication
if (project.properties['gitLabRepoAccess'] == null) {
    ext.gitLabRepoAccess = System.getenv("GL_REPO_ACCESS") ?: "NO_ACCESS"
}

publishing {
        
    repositories {
        maven {
            name "snapshot"
            url "https://oss.sonatype.org/content/repositories/snapshots/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }
        maven {
            name "release"
            url "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            credentials {
                username project.properties['sonatypeUsername'] ?: "nouser"
                password project.properties['sonatypePassword'] ?: "nopass"
            }
        }
        
        maven {
            name "GitLab"
            url "https://gitlab.com/api/v4/projects/24557189/packages/maven"
            credentials(HttpHeaderCredentials) {
                name = "Private-Token"
                value = gitLabRepoAccess
            }
            authentication {
                header(HttpHeaderAuthentication)
            }
        }
    }
}

// Additional configuration of publishing
apply plugin: ConfigurePublishing

configurePublishing {
    withPomXml {
        asNode().with {
            appendNode('url', 'http://mnlipp.github.io/jgrapes/')
            appendNode('scm').with {
                appendNode('url', 'scm:git@github.com:mnlipp/jgrapes-webconsole.git')
                appendNode('connection', 'scm:git@github.com:mnlipp/jgrapes-webconsole.git')
                appendNode('developerConnection', 'git@github.com:mnlipp/jgrapes-webconsole.git')
            }
            appendNode('licenses').with {
                appendNode('license').with {
                    appendNode('name', 'AGPL 3.0')
                    appendNode('url', 'https://www.gnu.org/licenses/agpl-3.0.en.html')
                    appendNode('distribution', 'repo')
                }
            }
            appendNode('developers').with {
                appendNode('developer').with {
                    appendNode('id', 'mnlipp')
                    appendNode('name', 'Michael N. Lipp')
                }
            }
        }
    }
}

processResources {
    dependsOn generatePomFileForMavenPublication
}

jar {
    into("META-INF/maven/$project.group/$project.name") {
        from generatePomFileForMavenPublication
        rename ".*", "pom.xml"
    }
}

build.mustRunAfter "release"

task listChangedFiles {
    doLast {
        def released = scmVersion.tag.prefix + scmVersion.previousVersion
        "git diff --name-only HEAD ${released} ${project.name}"
            .execute().text.split().each { println it }
    }
}

task listBuildChangedOnly {
    doLast {
        def released = scmVersion.tag.prefix + scmVersion.previousVersion
        def onlyBuild = !"git diff --name-only HEAD ${released} ${project.name}"
            .execute().text.split()
            .findAll { !it.endsWith('/build.gradle') }.any()
        if (onlyBuild) {
            println project.name
        }
    }
}

task listNeedsPublication {
    def result = !project.isSnapshot
    if (result) {
        def repo = "https://repo.maven.apache.org/maven2/"
        def url = repo + project.group.replace('.', '/') +
            "/" + project.name + "/" + version + "/" +
            project.name + "-" + version + ".pom"
        def code = new URL(url).openConnection().with {
            requestMethod = 'HEAD'
            connect()
            responseCode
        }
        result = (code == 404)
    }
    doLast {
        if (result) {
            println "${project.name} needs to be published."
        }
    }
}

task stageOnOssrh {
    group = "publishing"
    
    dependsOn "releaseTag"
    dependsOn "publishMavenPublicationToReleaseRepository"
}

task publishSnapshots {
    afterEvaluate {
        if (isSnapshot) {
            dependsOn 'publishMavenPublicationToSnapshotRepository'
        }
    }
}

afterEvaluate {
    if(!project.name.startsWith("org.jgrapes")) {
        tasks.matching { it.group == "publishing" }.all {
            enabled = false
        }
    }
}
