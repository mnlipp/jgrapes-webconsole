import org.gradle.api.tasks.testing.logging.TestExceptionFormat
import org.gradle.api.tasks.testing.logging.TestLogEvent

apply plugin: 'pl.allegro.tech.build.axion-release'
apply plugin: 'signing'
apply plugin: 'maven-publish'

group = 'org.jgrapes'

if (project.name.startsWith("org.jgrapes") 
    || project.name == "WebConsoleDemo") {

    apply plugin: 'java-library'
    apply plugin: 'biz.aQute.bnd.builder'
    apply plugin: 'org.ajoberstar.grgit'
    apply from: "${project.rootDir}/gradle/layout.gradle"

    scmVersion {
        versionIncrementer 'incrementMinor'
        tag {
            def shortened = project.name.startsWith(project.group + ".") ?
                project.name.substring(project.group.length() + 1) : project.name
            prefix = shortened.replace('.', '-') + "-"
        }
    }
    version = scmVersion.version
    ext.isSnapshot = version.endsWith('-SNAPSHOT')

    // Provide the Bundle-Version (derive from project version)
    def releaseVersion = version.replaceAll(/-SNAPSHOT$/, "")
    ext.bundleVersion = "${ -> releaseVersion +
        (isSnapshot ? '.-\${tstamp}-SNAPSHOT' : '.ga') }"

    if (project.name != "WebConsoleDemo") {
        apply from: "${project.rootDir}/gradle/baseline.gradle"
        apply from: "${project.rootDir}/gradle/javadoc.gradle"
        apply from: "${project.rootDir}/gradle/publishing.gradle"
    }
    
    java {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }

    jar {
        duplicatesStrategy = 'exclude'
        
        doFirst {
            manifest {
                attributes(
                    'Git-Descriptor': inputs.properties['gitDescriptor']
                )
            }
        }
        
        manifest {
            inputs.property("gitDescriptor", { grgit.describe(always: true) })
               
            // Set Git revision information in the manifests of built bundles
            attributes([
                "Git-Descriptor": grgit.describe(always: true),
                "Git-SHA": grgit.head().id,
                "Bundle-Version": bundleVersion
            ])
        }
        
        manifest {
            attributes('Bundle-Version': bundleVersion)
        }
    }
    
    dependencies {
        compileOnly 'org.osgi:org.osgi.annotation:6.0.0'
        testImplementation 'junit:junit:4.12', 'net.jodah:concurrentunit:0.4.2'
    }

    test {
    //    if (!rootProject.isCiBuild) {
    //        jvmArgs '-agentlib:jdwp=transport=dt_socket,server=y,suspend=n,address=18888'
    //    }
    
        testLogging {
            events TestLogEvent.FAILED,
                   TestLogEvent.SKIPPED,
                   TestLogEvent.PASSED,
                   TestLogEvent.STANDARD_ERROR,
                   TestLogEvent.STANDARD_OUT
    
            exceptionFormat TestExceptionFormat.FULL
    
            showStandardStreams = true
            showExceptions = true
            showStackTraces = true
            showCauses = true
        }
            
        if (isCiBuild) {
            maxParallelForks = 2
            maxHeapSize = '768m'
        }
    }
        
    task listSnapshots {
        doLast {
            if (isSnapshot) {
                println "Snapshot: " + project.name
            }
        }
    }
    
}

