import aQute.bnd.osgi.Processor
import java.util.regex.Pattern

buildscript {
    repositories {
        gradlePluginPortal()
    }
    dependencies {
        classpath 'biz.aQute.bnd:biz.aQute.bnd.gradle:6.1.0'
        classpath 'com.github.node-gradle:gradle-node-plugin:3.2.1'
        classpath 'org.ajoberstar.grgit:grgit-gradle:4.1.0'
        classpath 'org.ajoberstar:gradle-git-publish:3.0.0'
    }
}

plugins {
    id 'pl.allegro.tech.build.axion-release' version '1.13.6' apply false
}

apply plugin: 'org.ajoberstar.git-publish'
apply plugin: 'com.github.node-gradle.node'

ext {
	isCiBuild = System.getenv().get("CI") == 'true'
    isJitPackBuild = System.getenv().get("JITPACK") == 'true'
}

// Prepare github authentication for plugins
if (System.properties['org.ajoberstar.grgit.auth.username'] == null) {
	System.setProperty('org.ajoberstar.grgit.auth.username',
		System.getenv("GH_TOKEN") ?: project.properties['github.token'] ?: "nouser")
}

allprojects {
    repositories {
        mavenCentral()
        // Sometimes, synchronization with central is too slow
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        // Snapshots
        maven { url "https://oss.sonatype.org/content/repositories/snapshots/" }
        // Experimental, not sure if this is the way tpo go...
        maven { url 'https://jitpack.io' }
    }
}

subprojects {
    apply from: "${project.rootDir}/gradle/subprojects.gradle"
    // Must be applied after configuring the subprojects,
    // can therefore not be applied in allprojects.
    apply from: "${project.rootDir}/gradle/eclipse.gradle"
}

gitPublish {
	repoUri = 'https://github.com/mnlipp/jgrapes.git'
	branch = 'gh-pages'
	contents {
		from("${buildDir}/javadoc") {
			into 'javadoc-webconsole'
		}
        from("${buildDir}/aashdoc") {
            into 'aashdoc'
        }
	}
	preserve { include '**/*' }
	commitMessage = "Updated."
}

// Until https://github.com/ajoberstar/gradle-git-publish/issues/41 is fixed
tasks.gitPublishCopy.dependsOn(":org.jgrapes.webconsole.base:buildDoc")
tasks.gitPublishCopy.dependsOn(":aash-vue-components:buildDoc")

task stage {
    description = 'To be executed by CI, build and update JavaDoc.'
    group = 'build'

    // Build everything first
    subprojects.tasks.each { tc -> tc.findByName("build") }

    if (!isCiBuild || JavaVersion.current() == JavaVersion.VERSION_17) {
        // Publish JavaDoc
        dependsOn gitPublishPush
    }	
}

configure(stage) {
	description = 'To be executed by travis, build and update JavaDoc.'
	group = 'build'
}

apply from: "${project.rootDir}/gradle/eclipse.gradle"
