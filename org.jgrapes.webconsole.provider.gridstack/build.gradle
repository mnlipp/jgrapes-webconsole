dependencies {
	api project(':org.jgrapes.webconsole.base')
}

apply plugin: 'com.github.node-gradle.node'

task prepareNpmLibs(type: Copy) {
    dependsOn 'npmInstall'
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/provider/gridstack/gridstack')
    from ("node_modules/gridstack/dist/") {
        include '**/*'
        exclude 'src/**'
        exclude '**/*.js'
    }
    from ("node_modules/gridstack/dist/") {
        include '**/*.js'
        exclude 'src/**'
        // See https://github.com/gridstack/gridstack.js/issues/2476
        filter { line -> line.replaceAll(' from \'(.*)\'', ' from \'$1.js\'') }
    }
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn 'prepareNpmLibs'
}

eclipse {
    synchronizationTasks prepareNpmLibs
}
