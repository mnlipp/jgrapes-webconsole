plugins {
    id 'org.jgrapes.java-bundle-conventions'
    id 'com.github.node-gradle.node'
}

dependencies {
	api project(':org.jgrapes.webconsole.base')
}

task prepareNpmLibs(type: Copy) {
    dependsOn 'npmInstall'
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    into('build/generated/resources/org/jgrapes/webconsole/provider/moment/moment')
    from file("node_modules/moment/moment.js")
    from ("node_modules/moment/min/") {
        include '*'
    }
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn 'prepareNpmLibs'
}

eclipse {
    synchronizationTasks prepareNpmLibs
}
