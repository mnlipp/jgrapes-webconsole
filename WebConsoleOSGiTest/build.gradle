apply plugin: 'java'
apply plugin: 'biz.aQute.bnd.builder'

dependencies {
    runtimeOnly 'org.apache.felix:org.apache.felix.framework:7.0.3'
    runtimeOnly ('org.apache.aries.spifly:org.apache.aries.spifly.dynamic.bundle:1.3.4') {
        transitive = true }
    runtimeOnly 'org.apache.felix:org.apache.felix.gogo.command:1.1.2'
    runtimeOnly 'org.apache.felix:org.apache.felix.gogo.runtime:1.1.4'
    runtimeOnly 'org.apache.felix:org.apache.felix.gogo.shell:1.1.4'
    runtimeOnly (project(':WebConsoleTest')) { transitive = true }
}

release {
    enabled = false
}

// Nothing to compile, but avoids Eclipse warning.
java {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
}

// Not used due to:
//  - https://github.com/bndtools/bnd/issues/4998
//  - https://github.com/bndtools/bnd/issues/4997
// import aQute.bnd.gradle.Bndrun
//
// task run(type: Bndrun) {
//    group = 'verification'
//    workingDirectory = project.file('.')
//    bndrun = file("WebConsoleOSGiDemo.bndrun")
//    bundles = project.sourceSets.main.runtimeClasspath
//}

task createRunRepo(type: Copy) {
    from project.sourceSets.main.runtimeClasspath.files
    into new File(rootProject.buildDir, 'runRepo')
}

configurations {
    bndCli
}

dependencies {
    bndCli ('biz.aQute.bnd:biz.aQute.bnd:6.1.0') {
        transitive = true }
}

task run(type: JavaExec) {
    dependsOn createRunRepo
    
    classpath = configurations.bndCli
    main = 'aQute.bnd.main.bnd'
    args = ['WebConsoleOSGiDemo.bndrun']
    standardInput = System.in
}

// Proof of concept, best used with "--console plain"
task runGoGo(type: JavaExec) {
    dependsOn createRunRepo
    
    classpath = configurations.bndCli
    main = 'aQute.bnd.main.bnd'
    args = ['gogo.bndrun']
    standardInput = System.in
}

// For debugging
task runRepoList(type: JavaExec) {
    dependsOn createRunRepo
    
    classpath = configurations.bndCli
    main = 'aQute.bnd.main.bnd'
    args = ['repo', 'list']
    standardInput = System.in
}


eclipse {
    project {
       natures += 'bndtools.core.bndnature'
    }
    classpath {
        containers 'aQute.bnd.classpath.container'
    }
    jdt {
        javaRuntimeName = "JavaSE-11"
    }
}