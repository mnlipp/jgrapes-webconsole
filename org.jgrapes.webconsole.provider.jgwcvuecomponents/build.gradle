dependencies {
	api project(':org.jgrapes.webconsole.base')
    runtimeOnly project(':org.jgrapes.webconsole.provider.vue')
}

ext.releaseVersion = "0.7.0"
ext.isSnapshot = true
version = releaseVersion + (isSnapshot ? "-SNAPSHOT" : "")

apply plugin: 'com.github.node-gradle.node'

task prepareNpmLibs(type: Copy) {
    description '''
    Helper task that copies the the libraries from the npm_modules 
    directory into a package scoped subdirectory. The root is added 
    to eclipse as resource directory thus making the files available 
    as package scoped resources when running a process in eclipse.
    '''
    dependsOn ':aash-vue-components:buildLib'
    into('build/generated/resources/org/jgrapes/webconsole/provider/jgwcvuecomponents/aash-vue-components')
    from ("${rootProject.projectDir}/aash-vue-components/") {
        include 'lib/**/*'
        include 'src/**/*'
    }
}

sourceSets {
    main {
        resources {
            srcDir project.file('build/generated/resources')
        }
    }
}

processResources {
    dependsOn 'prepareNpmLibs'
}


eclipse {
    autoBuildTasks prepareNpmLibs
}

ext.jsDocumentJsDir = "${rootProject.buildDir}/javadoc/org/jgrapes/webconsole/provider/jgwcvuecomponents/jsdoc"

task documentjs(type: NodeTask) {
    dependsOn(':npmInstall')
    new File(jsDocumentJsDir).mkdirs()
    script = file("${rootProject.projectDir}/node_modules/documentation/bin/documentation.js")
    def input = "resources/org/jgrapes/webconsole/provider/jgwcvuecomponents/jgwc-vue-components/jgwc-components.js"
    inputs.file(file(input))
    outputs.dir project.file(jsDocumentJsDir)
    args = [
        "build",
        "-f", "html",
        "-o", jsDocumentJsDir, "--shallow",
        input
    ]
}

javadoc {
    if (!isJitPackBuild) {
        // Does not work on jitpack
        finalizedBy documentjs
    }
}
